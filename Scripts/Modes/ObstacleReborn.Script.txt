/**
 *	Obstacle Custom GameMode
 *  Authors : kana & lethan
 *  Original author : steeffeen
 */

#Extends "Modes/ShootMania/ModeBase.Script.txt"

#Const CompatibleMapTypes "ObstacleArena, ObstacleTitleArena, ObstacleRebornArena"

#Include "MathLib" as MathLib
#Include "TextLib" as TextLib
#Include "Libs/Nadeo/Message.Script.txt" as Message
//#Include "Libs/Nadeo/Interface.Script.txt" as Interface
#Include "Libs/Lethan/ChronoObs.Script.txt" as Chrono
#Include "Libs/Nadeo/ShootMania/SM.Script.txt" as SM
//#Include "Libs/Nadeo/ShootMania/Score.Script.txt" as Score
#Include "Libs/Nadeo/ShootMania/SpawnScreen.Script.txt" as SpawnScreen

// ---------------------------------- //
// Settings
// ---------------------------------- //

#Const Description _("Obstacle Custom GameMode\nOriginal Author : Steeffeen\nRework : kana & lethan")
#Const UITickPeriod 200

#Const Revision 6

// ---------------------------------- //
// Global var
// ---------------------------------- //
declare Text GamePhase;

declare CUILayer DebugInfo;
declare CUILayer[Ident] Times;

declare CSmMapLandmark MainSpawn;
declare CSmMapLandmark[] ChecksRaw;
declare CSmMapLandmark[Integer] Checkpoints;

declare Integer[Ident] StartTimes;
declare Integer[Ident] PreviousTimes;
declare Integer[Ident] BestTimes;
declare Integer BestTimeTheoretical;

declare Integer[Ident] CheckpointNext;
declare Integer[Integer] CheckpointsBest;
declare Integer[Ident] Respawns;

declare Integer CheckpointsCount;

// ---------------------------------- //
// Set rules
// ---------------------------------- //
***Rules***
***
declare ModeName = "Obstacle Reborn";
declare ModeRules = TextLib::Compose(_("Obstacle Custom GameMode\nOriginal Author : Steeffeen\nRework : kana & lethan"), "$"^SpawnScreen::GetModeColor());
SpawnScreen::ResetRulesSection();
SpawnScreen::AddSubsection(_("Obstacle Reborn GameMode"), ModeRules, 20.);
SpawnScreen::CreatePrettyRules(ModeName);
ModeStatusMessage = _("Obstacle Reborn GameMode\nOriginal Author : Steeffeen\nRework : kana & lethan");
***

// ---------------------------------- //
// Server start
// ---------------------------------- //
***StartServer***
***
// ---------------------------------- //
// Set mode options
UseClans = False;
UseAllies = False;
UsePvPCollisions = False;
UsePvPWeapons = False;

DebugInfo = UIManager.UILayerCreate();
UIManager.UIAll.UILayers.add(DebugInfo);
DebugInfo.ManialinkPage = """
	<frame posn="-158 88 0">
				<label posn="0 0 1" halign="left" style="TextButtonNav" text="Obstacle Reborn Revision : {{{Revision}}}" />
				
	</frame>
""";
// ---------------------------------- //
// Create the rules
---Rules---

// ---------------------------------- //
// Initialize UI
SpawnScreen::CreateScores("Score.RoundPoints");
SpawnScreen::CreateMapInfo();
//Interface::CreateRank();

// ---------------------------------- //
// Create the scores table
//ST2::SetStyle("LibST_SMBaseSolo");
//ST2::SetStyle("LibST_SMBasePoints");
//MB_SetScoresTableStyleFromXml(S_ScoresTableStylePath);
//ST2::Build("SM");
***


// ---------------------------------- //
// Map start
// ---------------------------------- //
***StartMap***
***
GamePhase = "init";

//Score::MatchBegin();
SM::SetupDefaultVisibility();
Chrono::Load();

InitObstacle();

// Init bases
foreach (Base in MapBases) {
	Base.Clan = 1;
	Base.IsActive = True;
}


UIManager.UIAll.UISequence = CUIConfig::EUISequence::Intro;
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::ForcedHidden;
***

// ---------------------------------- //
// Play loop
// ---------------------------------- //
***PlayLoop***
***
CheckForNewPlayers();
DoTheoreticalStuff();
foreach(Player, Players){
	UpdateUI(Player);
}
if(GamePhase == "init"){
	sleep(500);
	declare Text authorMsg = "Author : " ^ Map.AuthorNickName ^ " (" ^ Map.AuthorLogin ^ ")";
	Message::SendStatusMessage(authorMsg, 5000, 3, CUIConfig::EUISound::Silence, 0);
	Message::SendBigMessage(MapName, 5000, 3, CUIConfig::EUISound::EndRound, 0);

	sleep(1000);
	
	StartTime = Now;
	EndTime = -1;
	
	UIManager.UIAll.UISequence = CUIConfig::EUISequence::Playing;
		
	GamePhase = "play";
	
	
}
if(GamePhase == "play"){
	//Players
	foreach(Player, Players){		
		if(StartTimes.existskey(Player.Id) && Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned){
			if(CheckpointNext[Player.Id] == 0){
				COSpawnPlayer(Player);
			}
		}
		if(Player.SpawnStatus == CSmPlayer::ESpawnStatus::Spawned) {
			if(StartTimes.existskey(Player.Id)){
				if(CheckpointNext[Player.Id] == 0){
					Chrono::Start(Player.Id, 0);
					StartTimes[Player.Id] = Now;
					CheckpointNext[Player.Id] = 1;
					
					UpdateMarkerCheckpoint(Player, False);
				}
			}
		}
		
		if(Player.CapturedLandmark != Null){
			if(TextLib::SubText(Player.CapturedLandmark.Tag, 0, 4) == "Goal"){
				if(CheckpointNext[Player.Id] == CheckpointsCount){
					FinishPass(Player);
				}
			}
			if(TextLib::SubText(Player.CapturedLandmark.Tag, 0, 10) == "Checkpoint"){
				if(Player.CapturedLandmark.Order == CheckpointNext[Player.Id] && CheckpointNext[Player.Id] == CheckpointsCount){
					FinishPass(Player);
				} else {
					if(Player.CapturedLandmark.Order == CheckpointNext[Player.Id]){
						CheckpointPass(Player);
					}
				}
			}			
		}
	}
	
	//Events
	foreach(Event, PendingEvents){
		switch(Event.Type){
			case CSmModeEvent::EType::OnPlayerRequestRespawn : {
				CORespawnPlayer(Event.Player);
			}
			case CSmModeEvent::EType::OnArmorEmpty : {
				Discard(Event);
				CORespawnPlayer(Event.Victim);
			}
		}
	}
	
}

Message::Loop();
***


// ---------------------------------- //
// Map end
// ---------------------------------- //
/****EndMap***
***
****/
// ---------------------------------- //
// Server end
// ---------------------------------- //
***EndServer***
***
UIManager.UILayerDestroyAll();
//Interface::DestroyRank();
SpawnScreen::DestroyRules();
SpawnScreen::DestroyScores();
SpawnScreen::DestroyMapInfo();
***

// ---------------------------------- //
//Functions
// ---------------------------------- //


//Calculate Theoretical Time
Void DoTheoreticalStuff(){
	declare _BestTimeTheoretical = 0;
		
	foreach(BestCheck, CheckpointsBest){
		_BestTimeTheoretical += BestCheck;
	}
		
	BestTimeTheoretical = _BestTimeTheoretical;
}

//Update 3d markers for player
Void UpdateMarkerCheckpoint(CSmPlayer _Player, Boolean _Finish){

	declare UI <=> UIManager.GetUI(_Player); //Get UI for player
	declare cpNext = CheckpointNext[_Player.Id]; //Get next checkpoint
	declare Integer[Integer] CheckpointsTimes for _Player;	//Get checkpoints time of the player

	declare cpNextTime = "";
	
	if(CheckpointsTimes.existskey(CheckpointNext[_Player.Id])){
		cpNextTime = " : (" ^ TextLib::TimeToText(CheckpointsTimes[cpNext], True) ^ ")"; 
	}
	
	
	if(!_Finish){
		if(cpNext != CheckpointsCount){
			UI.MarkersXML = """<marker label="CP {{{cpNext}}}{{{cpNextTime}}}" pos="{{{Checkpoints[cpNext].Position.X}}} {{{Checkpoints[cpNext].Position.Y + 2}}} {{{Checkpoints[cpNext].Position.Z}}}" />""";
		} else {
			UI.MarkersXML = """<marker label="Finish{{{cpNextTime}}}" pos="{{{Checkpoints[cpNext].Position.X}}} {{{Checkpoints[cpNext].Position.Y + 2}}} {{{Checkpoints[cpNext].Position.Z}}}" />""";
		}
	} else {
		UI.MarkersXML = "";
	}
}

Void UpdateUI(CSmPlayer _Player){
	declare Integer LastUIUpdate for _Player;
	
	if(Now > LastUIUpdate + UITickPeriod){
		LastUIUpdate = Now;
		declare UI <=> UIManager.GetUI(_Player);
	
		declare Text BestTimeTheoreticalText;
		declare Text BestLastCP;
		declare Text BestNextCP;
		
		declare Text BestTimePersonal;
		declare Text PreviousTimePersonal;
		
		if(BestTimeTheoretical != 0){
			BestTimeTheoreticalText = TextLib::TimeToText(BestTimeTheoretical, True);
		} else {
			BestTimeTheoreticalText = "--:--.--";
		}
		
		if(CheckpointNext[_Player.Id] > 1){
			if(CheckpointsBest.existskey(CheckpointNext[_Player.Id] - 1)){
				BestLastCP = TextLib::TimeToText(CheckpointsBest[CheckpointNext[_Player.Id] - 1], True);
			} else {
				BestLastCP = "--:--.--";
			}
		} else {
			BestLastCP = "--:--.--";
		}
		
		if(CheckpointsBest.existskey(CheckpointNext[_Player.Id])){
			BestNextCP = TextLib::TimeToText(CheckpointsBest[CheckpointNext[_Player.Id]], True);
		} else {
			BestNextCP = "--:--.--";			
		}
		
		if(PreviousTimes[_Player.Id] != 0){
			PreviousTimePersonal = TextLib::TimeToText(PreviousTimes[_Player.Id], True);
		} else {
			PreviousTimePersonal = "--:--.--";	
		}
		
		if(BestTimes[_Player.Id] != 0){
			BestTimePersonal = TextLib::TimeToText(BestTimes[_Player.Id], True);
		} else {
			BestTimePersonal = "--:--.--";	
		}
	
		declare check = CheckpointNext[_Player.Id] - 1;
		
		if(check < 0) check = 0;
	
		UI.ManialinkPage = """
		<label posn="-48 -76 0" halign="center" valign="center" style="TextButtonMedium" text="Progress : {{{check}}}/{{{CheckpointsCount}}}"/>
		<label posn="0 -65 0" halign="center" valign="center" style="TextButtonMedium" text="Respawns : {{{Respawns[_Player.Id]}}}"/>
		<frame posn="130 -55 0" scale="0.8">
		<label posn="-17 37 0" sizen="20 5" style="TextButtonMedium" text="All" id="All"/>
		<quad posn="-20 36 0" sizen="60 33" bgcolor="FFFA" style="Bgs1" substyle="BgButtonGrayed"/>
		<label posn="-18 30 0" sizen="22 5" text="Best time theoretical" id="BestTimeAll"/>
		<label posn="-17 21 0" sizen="20 5" text="Last CP" id="LastCP"/>
		<label posn="-17 12 0" sizen="20 5" text="Next CP" id="NextCP"/>
		<label posn="22 28 0" sizen="34 9" text="$f90{{{BestTimeTheoreticalText}}}" style="TextRaceChrono" textsize="5" halign="center" valign="center" id="bestAll"/>
		<label posn="22 19 0" sizen="34 9" text="$ff3{{{BestLastCP}}}" style="TextRaceChrono" textsize="5" halign="center" valign="center" id="lastcp"/>
		<label posn="22 10 0" sizen="34 9" text="$619{{{BestNextCP}}}" style="TextRaceChrono" textsize="5" halign="center" valign="center" id="nextcp"/>
		
		<label posn="-17 2 0" sizen="20 5" style="TextButtonMedium" text="You" id="You"/>
		<quad posn="-20 1 0" sizen="60 22" bgcolor="FFFA" style="Bgs1" substyle="BgButtonGrayed"/>
		<label posn="-17 -3 0" sizen="20 5" text="Best time" id="BestTime"/>
		<label posn="-17 -12 0" sizen="20 5" text="Previous" id="Previous"/>
		<label posn="22 -5 0" sizen="34 9" text="$0f0{{{BestTimePersonal}}}" style="TextRaceChrono" textsize="5" halign="center" valign="center" id="best"/>
		<label posn="22 -14 0" sizen="34 9" text="$050{{{PreviousTimePersonal}}}" style="TextRaceChrono" textsize="5" halign="center" valign="center" id="prev"/>
		<label posn="-10 -24 0" sizen="20 5" style="TextButtonMedium" text="Time" id="time"/>
	</frame>
		""";
	}
}

Void CheckForNewPlayers(){
	foreach(Player, Players){	
		if(!StartTimes.existskey(Player.Id)) {
			StartTimes[Player.Id] = 0;
			CheckpointNext[Player.Id] = 0;
			PreviousTimes[Player.Id] = 0;
			BestTimes[Player.Id] = 0;
			Respawns[Player.Id] = 0;
			
			declare Integer[Integer] CheckpointsTimes for Player;
			declare Integer[Integer] CheckpointsDiff for Player;
			CheckpointsDiff = Integer[Integer];
			CheckpointsTimes = Integer[Integer];
			
			Chrono::Create(Player.Id);
		}
	}
}

Void InitObstacle(){
	CheckpointsCount = 0;
	ChecksRaw.clear();
	StartTimes.clear();
	
	CheckpointsBest = Integer[Integer];
	
	UIManager.UIAll.OverlayHideMapInfo = False;
	
	//Get Spawns
	foreach(Spawn, MapLandmarks_PlayerSpawn){
		if(TextLib::SubText(Spawn.Tag, 0, 5) == "Spawn"){
			MainSpawn <=> Spawn;
		}
	}
	
	//Get checkpoint count
	foreach(Checkpoint, MapLandmarks_Gauge){	
		if(TextLib::SubText(Checkpoint.Tag, 0, 10) == "Checkpoint"){
			ChecksRaw.add(Checkpoint);
			CheckpointsCount += 1;
		}
	}
	
	//Reorganise
	for(i, 1, CheckpointsCount){
		declare CSmMapLandmark nextCheck;
	
		foreach(CurCheck, ChecksRaw){
			//First one
			if(nextCheck == Null){
				if(TextLib::SubText(CurCheck.Tag, 0, 10) == "Checkpoint"){
					nextCheck <=> CurCheck;
				}
			} else {
				if(CurCheck.Order < nextCheck.Order && TextLib::SubText(CurCheck.Tag, 0, 10) == "Checkpoint"){
					nextCheck <=> CurCheck;
				}
			}
		}
		
		Checkpoints[i] <=> nextCheck;
		declare removed = ChecksRaw.remove(nextCheck);
	}
	
	//Add goal retro compatibility
	if(Map.MapType != "ObstacleRebornArena"){
		foreach(FinishFind, MapLandmarks_Gauge){
			if(FinishFind.Tag == "Goal"){
				CheckpointsCount += 1;
				Checkpoints[CheckpointsCount] <=> FinishFind;
			}
		}
	}
}

Void COSpawnPlayer(CSmPlayer _Player){
	_Player.ArmorMax = 100;
	SpawnPlayer(_Player, 0, 100, MainSpawn.PlayerSpawn, Now);
}

Void CORespawnPlayer(CSmPlayer _Player){
	if(CheckpointNext[_Player.Id] == 1){
		UnspawnPlayer(_Player);
		Chrono::Stop(_Player.Id);
		CheckpointNext[_Player.Id] = 0;
	} else {
		RespawnPlayer(_Player, Checkpoints[CheckpointNext[_Player.Id] - 1]);
		Respawns[_Player.Id] += 1;
	}
}

Void CheckpointStuff(CSmPlayer _Player){
	declare Integer[Integer] CheckpointsTimes for _Player;	//Store best checks for this player
	declare Integer[Integer] CheckpointsDiff for _Player;

	declare Integer timeLastCheckpoint for _Player;
	declare Integer timeDiffCheckpoint for _Player;
	
	declare Integer soundCheckpoint for _Player;
	
	soundCheckpoint = 2;
	
	//Best check player
	if(!CheckpointsTimes.existskey(CheckpointNext[_Player.Id])){ //First run
		CheckpointsTimes[CheckpointNext[_Player.Id]] = Now - StartTimes[_Player.Id];
		timeLastCheckpoint = 0;
	} else {
		timeLastCheckpoint = (Now - StartTimes[_Player.Id]) - CheckpointsTimes[CheckpointNext[_Player.Id]];
		soundCheckpoint = 0;
		if(timeLastCheckpoint < 0){
			CheckpointsTimes[CheckpointNext[_Player.Id]] = Now - StartTimes[_Player.Id];
			soundCheckpoint = 1;
		}
	}
	
	//Best check diff

	if(CheckpointNext[_Player.Id] > 1){
		timeDiffCheckpoint = CheckpointsTimes[CheckpointNext[_Player.Id]] - CheckpointsTimes[CheckpointNext[_Player.Id] - 1];
	} else {
		timeDiffCheckpoint = Now - StartTimes[_Player.Id];		
	}
	CheckpointsDiff[CheckpointNext[_Player.Id]] = timeDiffCheckpoint;
	
}

//When a player passes checkpoint
Void CheckpointPass(CSmPlayer _Player){
	
	declare Integer[Integer] CheckpointsTimes for _Player;	//Store best checks for this player
	declare Integer[Integer] CheckpointsDiff for _Player;

	declare Integer timeLastCheckpoint for _Player;
	declare Integer timeDiffCheckpoint for _Player;

	declare Integer soundCheckpoint for _Player;

	CheckpointStuff(_Player);

	declare soundBestAllCheckpoint = False;
	
	declare Text timeLastCheckText;
	declare Text timeDiffCheckText;
	
	timeLastCheckText = "";
	timeDiffCheckText = "";

	if(timeLastCheckpoint != 0){
		if(timeLastCheckpoint < 0){
			timeLastCheckText = " $fff($36f" ^ TextLib::TimeToText(timeLastCheckpoint, True) ^ "$fff)";
		} else if(timeLastCheckpoint > 0) {
			timeLastCheckText = " $fff($a00+" ^ TextLib::TimeToText(timeLastCheckpoint, True) ^ "$fff)";
		}
	}
	
	

	//Best
	if(!CheckpointsBest.existskey(CheckpointNext[_Player.Id])){
		CheckpointsBest[CheckpointNext[_Player.Id]] = CheckpointsDiff[CheckpointNext[_Player.Id]];
	} else {
		if(CheckpointsDiff[CheckpointNext[_Player.Id]] < CheckpointsBest[CheckpointNext[_Player.Id]]){
			CheckpointsBest[CheckpointNext[_Player.Id]] = CheckpointsDiff[CheckpointNext[_Player.Id]];
			soundBestAllCheckpoint = True;
		}
	}
	
	declare time = TextLib::TimeToText(Now - StartTimes[_Player.Id], True);
	
	Message::SendStatusMessage(_Player, "Checkpoint " ^ CheckpointNext[_Player.Id] ^ "/" ^ CheckpointsCount, 5000, 3, CUIConfig::EUISound::ScoreProgress, soundCheckpoint);
		
	if(soundBestAllCheckpoint){
		Message::SendBigMessage(_Player, time ^ timeLastCheckText, 5000, 3, CUIConfig::EUISound::TiePoint, 0);
	} else {
		Message::SendBigMessage(_Player, time ^ timeLastCheckText, 5000, 3, CUIConfig::EUISound::Silence, 0);
	}
	
	CheckpointNext[_Player.Id] += 1;
	UpdateMarkerCheckpoint(_Player, False);
}

//When a player passes finish
Void FinishPass(CSmPlayer _Player){

	declare Integer[Integer] CheckpointsDiff for _Player;

	declare time = Now - StartTimes[_Player.Id];
	declare diff = time - BestTimes[_Player.Id];	
	declare newRecord = False;
	declare Text diffText;
	
	CheckpointStuff(_Player);
	
	PreviousTimes[_Player.Id] = time;
	
	if(BestTimes[_Player.Id] == 0){
		BestTimes[_Player.Id] = PreviousTimes[_Player.Id];
		diffText = "";
	} else {
		if(PreviousTimes[_Player.Id] < BestTimes[_Player.Id]){
			diff = time - BestTimes[_Player.Id];
			BestTimes[_Player.Id] = PreviousTimes[_Player.Id];
			newRecord = True;
			
			diffText = " ($0f0" ^ TextLib::TimeToText(diff, True) ^ "$fff)";
		} else {
			diffText = " ($f00+" ^ TextLib::TimeToText(diff, True) ^ "$fff)";
		}
	}
	
	//Best
	if(!CheckpointsBest.existskey(CheckpointNext[_Player.Id])){
		CheckpointsBest[CheckpointNext[_Player.Id]] = CheckpointsDiff[CheckpointNext[_Player.Id]];
	} else {
		if(CheckpointsDiff[CheckpointNext[_Player.Id]] < CheckpointsBest[CheckpointNext[_Player.Id]]){
			CheckpointsBest[CheckpointNext[_Player.Id]] = CheckpointsDiff[CheckpointNext[_Player.Id]];
		}
	}
				
	Message::SendStatusMessage(_Player, "Race completed. " ^ TextLib::ToText(Respawns[_Player.Id]) ^ " respawns." , 4000, 3, CUIConfig::EUISound::Silence, 1);
	if(newRecord){
		Message::SendBigMessage(_Player, "New record : " ^ TextLib::TimeToText(time, True) ^ diffText, 4000, 3, CUIConfig::EUISound::VictoryPoint, 0);
	} else {
		Message::SendBigMessage(_Player, "Time : " ^ TextLib::TimeToText(time, True) ^ diffText, 4000, 3, CUIConfig::EUISound::EndMatch, 0);
	}
	
	//Reset player
	UpdateMarkerCheckpoint(_Player, True);
	UnspawnPlayer(_Player);
	Chrono::Stop(_Player.Id);
	Chrono::Reset(_Player.Id);
	CheckpointNext[_Player.Id] = 0;
	Respawns[_Player.Id] = 0;
}